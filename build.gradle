plugins {
    id 'java'
    id 'distribution'
    id 'com.github.jk1.dependency-license-report' version '2.0'
}

group 'org.jlab.kafka.connect'
version '1.7.1'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        java.srcDir "${projectDir}/src/integration/java"
        resources.srcDir "${projectDir}/src/integration/resources"
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    testImplementation.extendsFrom compileOnly
    integrationImplementation.extendsFrom testImplementation
    integrationRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.epics:jca:2.4.6',
            'org.jlab:kafka-common:1.0.0'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    compileOnly 'org.apache.kafka:connect-api:3.1.0'
    compileOnly 'org.slf4j:slf4j-log4j12:1.7.36'
    testImplementation 'junit:junit:4.13.2'
    integrationImplementation 'org.apache.kafka:kafka-clients:3.1.0'
    integrationImplementation 'org.slf4j:slf4j-log4j12:1.7.36'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests using existing BOOTSTRAP_SERVERS env.'
    group = 'verification'

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    testLogging {
        showStandardStreams = true
    }
}

task integrationTestOutsideNetwork(type: Test) {
    description = 'Runs integration tests using Kafka OUTSIDE Advertised Listener explicit BOOTSTRAP_SERVERS.'
    group = 'verification'

    environment "BOOTSTRAP_SERVERS", "localhost:9094"
    environment "EPICS_CA_ADDR_LIST", "localhost:5064"

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}


test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jar {
    archiveFileName = 'epics2kafka.jar'
}

processIntegrationResources {
    duplicatesStrategy 'include'
}

processResources {
    filesMatching('release.properties') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
    }
}

build {
    dependsOn(installDist)
}

installDist.dependsOn(generateLicenseReport)
distTar.dependsOn(generateLicenseReport)
distZip.dependsOn(generateLicenseReport)


distributions {
    main {
        contents {
            from jar
            from (project.configurations.runtimeClasspath)
            from("build/reports/dependency-license") {
                into "dependency-license"
            }
        }
    }
}