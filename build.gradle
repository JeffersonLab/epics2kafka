plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

group 'org.jlab.kafka.connect'
version '0.8.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/slominskir/maven"
    }
}

// We define two extra sets: integration tests and alarms (docker-compose alarms example dependencies)
sourceSets {
    integration {
        java.srcDir "${projectDir}/src/integration/java"
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    alarms {
        // No source, just dependencies
    }
}

configurations {
    testImplementation.extendsFrom compileOnly
    integrationImplementation.extendsFrom implementation
    integrationRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.epics:jca:2.4.3' // Newer versions in mavenCentral only work with Java 11
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.2'
    compileOnly group: 'org.apache.kafka', name: 'connect-api', version: '2.4.0'
    compileOnly group: 'org.apache.kafka', name: 'connect-transforms', version: '2.4.0'
    compileOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    alarmsImplementation group: 'org.jlab.kafka.connect.transform', name: 'kafka-transform-epics', version: '0.15.0'
    integrationImplementation "org.testcontainers:testcontainers:1.14.3"
    integrationImplementation 'org.slf4j:slf4j-simple:1.6.1'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    //shouldRunAfter test

    testLogging {
        showStandardStreams = true
    }
}

//check.dependsOn integrationTest

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    archiveFileName = 'epics2kafka.jar'
}

processResources {
    filesMatching('release.properties') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
    }
}

build {
    dependsOn(installDist)
}

task alarmTest(type: Copy) {
    dependsOn(build)
    into "${buildDir}/install"
    from configurations.alarmsCompileClasspath
    include '**/kafka-transform-epics-*.jar'
}

distributions {
    main {
        contents {
            from jar
            from (project.configurations.runtimeClasspath)
        }
    }
}